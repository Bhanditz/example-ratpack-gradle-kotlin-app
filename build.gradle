buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.15",
        "com.github.jengelman.gradle.plugins:shadow:1.2.1",
        "org.jetbrains.kotlin:kotlin-gradle-plugin:0.11.91.1"
  }
}

if (!JavaVersion.current().java8Compatible) {
  throw new IllegalStateException("Must be built with Java 8 or higher")
}

apply plugin: "io.ratpack.ratpack-java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "kotlin"

repositories {
  maven { url "http://clinker.netty.io/nexus/content/repositories/snapshots" }
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:0.11.91.1"
  compile ratpack.dependency("guice")
  testCompile "junit:junit:4.12"
}

compileKotlin {
  kotlinOptions.annotations = file("annotations/ratpack")
}

apply plugin: "idea"

idea {
  project {
    //use JDK 1.8 in idea
    jdkName "1.8"
    languageLevel "1.8"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        //configure git support for the project in idea
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
      }
    }
  }
}

mainClassName = "ratpack.example.kotlin.KotlinPackage"