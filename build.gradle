buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.19",
        "com.github.jengelman.gradle.plugins:shadow:1.2.1",
        "org.jetbrains.kotlin:kotlin-gradle-plugin:0.12.1230"
  }
}

if (!JavaVersion.current().java8Compatible) {
  throw new IllegalStateException("Must be built with Java 8 or higher")
}

apply plugin: "io.ratpack.ratpack-java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "kotlin"
apply plugin: "idea"

repositories {
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:0.12.1230"
  compile ratpack.dependency("guice")
  testCompile "junit:junit:4.12"
}

idea {
  project {
    jdkName "1.8"
    languageLevel "JDK_1_8"
    vcs = "Git"
    ipr {
      withXml {
        def node = it.asNode()
        def parser = new XmlParser()
        node.append(parser.parse(file("${rootDir}/gradle/idea/codeStyleSettings.xml")))
        node.append(parser.parse(file("${rootDir}/gradle/idea/gradleSettings.xml")))
      }
    }
  }
}

mainClassName = "ratpack.example.kotlin.KotlinPackage"
